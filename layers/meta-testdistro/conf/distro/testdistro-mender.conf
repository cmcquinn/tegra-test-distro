include testdistro.conf

REQUIRED_TD_BBLAYERS_CONF_VERSION = "${DISTRO}-7"

DISTRO = "testdistro-mender"
DISTRO_NAME = "Test Distro with Mender"
DISTRO_CODENAME .= "+mender"

# The distro version doesn't include DATE to avoid basehash changes in do_image_mender
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot"

# For use in the /etc/os-release rewriter in
# mender-workarounds.bbclass
OS_RELEASE_VERSION ?= "${BUILDNAME} (${DISTRO_CODENAME})"

### Additions for mender ###
# distro_layer_buildinfo added to provide git hash in BUILDNAME
# (and thus MENDER_ARTIFACT_NAME)
INHERIT += "mender-full"
INHERIT:append:tegra = " tegra-mender-setup mender-workarounds mendersign"
# DISTRO_LAYER defaults to "meta-${DISTRO}", which is not correct here.
DISTRO_LAYER = "meta-testdistro"

BUILDNAME ?= "${@d.getVar('DISTRO_VERSION').replace('+snapshot','')}${DISTRO_LAYER_BUILDTAG}"
BUILDNAME[vardepvalue] = "${DISTRO_VERSION}"

MENDER_ARTIFACT_NAME ?= "${IMAGE_BASENAME}_${BUILDNAME}"

TEGRA_IMAGE_FSTYPES = "tar.gz ext4.gz tegraflash mender dataimg"
TEGRA_IMAGE_FSTYPES:cryptparts = "tar.gz ext4.gz mender dataimg"
IMAGE_FSTYPES:tegra = "${TEGRA_IMAGE_FSTYPES}"
IMAGE_FSTYPES:pn-tegra-minimal-initramfs:tegra = "${INITRAMFS_FSTYPES}"

# Reserved space for tegraflash partitions, not including APP and DATA
TEGRA_FLASH_PARTITIONS_MB ?= "950"

MENDER_STORAGE_DEVICE_BASE ?= "/dev/mmcblk0p"
MENDER_STORAGE_TOTAL_SIZE_MB ?= "${@tegra_mender_calc_total_size(d) - int(d.getVar('TEGRA_FLASH_PARTITIONS_MB'))}"

IMAGE_INSTALL:append:tegra = " tegra-eeprom-tool i2c-tools tegra-bup-payload"

# 5046 = 950 (default from above) plus 4096 for the swap partition
TEGRA_FLASH_PARTITIONS_MB:jetson-nano-2gb-devkit = "5046"
MENDER_DATA_PART_NUMBER:jetson-nano-devkit = "3"
MENDER_ROOTFS_PART_B_NUMBER:jetson-nano-devkit = "2"
# Swap is in partition 3 on the 2GB devkit
MENDER_DATA_PART_NUMBER:jetson-nano-2gb-devkit = "4"
MENDER_ROOTFS_PART_B_NUMBER:jetson-nano-2gb-devkit = "2"
# We have a custom flash layout for TX2
MENDER_ROOTFS_PART_B_NUMBER:jetson-tx2-devkit = "2"
MENDER_DATA_PART_NUMBER:jetson-tx2-devkit = "3"

# Use cboot by default on TX2 to avoid uboot slot alignment issues
PREFERRED_PROVIDER_virtual/bootloader:tegra186 = "cboot-t18x"

# Use u-boot by default on the TX2 COT when using the FIT image
PREFERRED_PROVIDER_virtual/bootloader:jetson-tx2-devkit-cot = "u-boot-tegra"

# Required for handling downgrades to versions that still use
# the NVIDIA stock bootloader tools, which use the nvbootctrl
# command in downloaded ArtifactInstall scripts.
IMAGE_INSTALL:append:tegra = " tegra-boot-tools-nvbootctrl"

MENDER_FEATURES_ENABLE:remove:cryptparts = "mender-growfs-data"

# Machine-specific overrides for "semi-stateless" setup:
#   - expect UDA (/data) partition to be 16MB
MENDER_FEATURES_ENABLE:remove:semi-stateless = "mender-growfs-data"
MENDER_DATA_PART_SIZE_MB_DEFAULT:semi-stateless = "16"
